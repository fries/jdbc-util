buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

group = 'de.schaeuffelhut'
//version = '0.3.2-SNAPSHOT'
version = '0.3.2'

apply plugin: 'java'
apply plugin: 'osgi'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compile('log4j:log4j:1.2.15') { transitive = false }
    compile 'joda-time:joda-time:1.6.2'
    testCompile 'junit:junit-dep:4.10'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

jar {
    manifest {
        name = 'JdbcUtil'
        instruction 'Import-Package', 'javax.naming;resolution:=optional'
        instruction 'Import-Package', 'javax.net;resolution:=optional'
        instruction 'Import-Package', 'javax.net.ssl;resolution:=optional'

        instruction 'Bundle-Vendor', 'Friedrich Schaeuffelhut'
        instruction 'Bundle-Description', 'JDBC Utilities'
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war'))
            {
                from components.web
            } else
            {
                from components.java
            }

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = bintrayUserName       //this usually comes form gradle.properties file in ~/.gradle
    key = bintrayApiKey         //this usually comes form gradle.properties file in ~/.gradle
    publications = ['mavenJava'] // see publications closure
    pkg {                        //package will be created if does not exist
        repo = 'maven'
//      userOrg = 'myorg'        // an optional organization name when the repo belongs to one of the user's orgs
        name = 'de.schaeuffelhut.jdbc'
        desc = 'JdbcUtil'
        licenses = ['Apache-2.0']
        labels = ['java', 'jdbc']
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

