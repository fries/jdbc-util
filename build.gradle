/*
 * Copyright (c) 2009-2025 the JdbcUtil authors
 *
 * SPDX-License-Identifier: MIT
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.gorylenko.gradle-git-properties'
    id 'org.owasp.dependencycheck'
}

version = '0.5.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api libs.joda.time

    compileOnly libs.jetbrains.annotations

    implementation libs.slf4j

    testCompileOnly libs.jetbrains.annotations

    testImplementation libs.assertj.core
    testImplementation libs.h2
    testImplementation libs.junit.jupiter
    testImplementation libs.junit.vintage.engine
    testRuntimeOnly libs.junit.platform.launcher
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            groupId = 'de.schaeuffelhut.jdbc-util'
            artifactId = 'jdbc-util'

            pom {
                name = 'jdbc-util'
                description = 'Core JDBC utility library â€“ connection helpers, statement wrappers, result mappers.'
                url = 'https://github.com/fries/jdbc-util'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        name = 'Friedrich Schaeuffelhut'
                        email = 'fries@desert.lnp.org'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:fries/jdbc-util.git'
                    developerConnection = 'scm:git:ssh://github.com/fries/jdbc-util.git'
                    url = 'https://github.com/fries/jdbc-util'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'central'
            url = 'https://central.sonatype.com/api/publishing/deployments/maven2/'
            credentials {
                username = project.findProperty("sonatypeUsername") as String
                password = project.findProperty("sonatypePassword") as String
            }
        }

        maven {
            name = "codeSbg"
            url = uri("https://code.schaeuffelhut-berger.de/api/packages/oss/maven")

            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "token " + project.findProperty("codeSbgPassword") as String
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

signing {
    useGpgCmd()
    // Set signing.gnupg.keyName= in gradle.properties
    sign publishing.publications.maven
}

// https://docs.gradle.org/current/userguide/gradle_wrapper.html
// To update the gradle wrapper use:
// ./gradlew wrapper  --distribution-type=all --gradle-version latest
