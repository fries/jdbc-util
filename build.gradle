/*
 * Copyright (c) 2009-2025 the JdbcUtil authors
 *
 * SPDX-License-Identifier: MIT
 */
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.gorylenko.gradle-git-properties'
    id 'org.owasp.dependencycheck'
    id 'com.vanniktech.maven.publish' version '0.36.0'
}

version = '1.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

javadoc {
    // lots of repetitive auto generated object() methods without javadoc
    exclude '**/ResultSetMappers.java'
    exclude '**/Legacy**.java'
}
repositories {
    mavenCentral()
}

dependencies {
    api libs.joda.time

    compileOnly libs.jetbrains.annotations

    implementation libs.slf4j

    testCompileOnly libs.jetbrains.annotations

    testImplementation libs.assertj.core
    testImplementation libs.h2
    testImplementation libs.junit.jupiter
    testImplementation libs.junit.vintage.engine
    testRuntimeOnly libs.junit.platform.launcher
}

test {
    useJUnitPlatform()
}

mavenPublishing {
    publishToMavenCentral()  // this enables Central Portal ZIP bundle upload

    // Optional: auto-publish after validation (start without it for safety)
    // configureMavenCentral { autoPublish = true }

    coordinates('de.schaeuffelhut.jdbc','jdbc-util', project.version);

    pom {
        name = 'jdbc-util'
        description = 'Core JDBC utility library â€“ connection helpers, statement wrappers, result mappers.'
        url = 'https://github.com/fries/jdbc-util'

        licenses {
            license {
                name = 'MIT License'
                url = 'https://opensource.org/licenses/MIT'
            }
        }

        developers {
            developer {
                name = 'Friedrich Schaeuffelhut'
                email = 'fries@desert.lnp.org'
            }
        }

        scm {
            connection = 'scm:git:git@github.com:fries/jdbc-util.git'
            developerConnection = 'scm:git:ssh://git@github.com/fries/jdbc-util.git'
            url = 'https://github.com/fries/jdbc-util'
        }
    }
}

tasks.matching { it.name.contains('generateMetadataFile') }.configureEach { dependsOn 'plainJavadocJar' }

signing {
    useGpgCmd()
    sign publishing.publications.withType(org.gradle.api.publish.maven.MavenPublication)
}
//publishing {
//    repositories {
//        maven {
//            name = "codeSbg"
//            url = uri("https://code.schaeuffelhut-berger.de/api/packages/oss/maven")
//            credentials(HttpHeaderCredentials) {
//                name = "Authorization"
//                value = "token " + project.findProperty("codeSbgPassword") as String
//            }
//            authentication {
//                header(HttpHeaderAuthentication)
//            }
//        }
//    }
//}
